import cc::lang as cc;

type UINT = cc::intUnsigned;
type UINT_PTR = cc::uint64;
type LONG_PTR = cc::int64;

type WPARAM = UINT_PTR;
type LPARAM = LONG_PTR;
type LRESULT = LONG_PTR;

type HINSTANCE = opaque;
type HANDLE = opaque;
type HICON = opaque;
type HCURSOR = opaque;
type HBRUSH = opaque;
type HMENU = opaque;
type HWND = opaque;

type LPCSTR = str;
type WORD = cc::shortUnsigned;
type ATOM = WORD;
type BOOL = cc::int;

type WNDPROC = def(HWND, UINT, WPARAM, LPARAM) -> LRESULT;

@layout(platform)
struct WNDCLASSA {
    style: UINT;
    lpfnWndProc: WNDPROC;
    cbClsExtra: cc::int;
    cbWndExtra: cc::int;
    hInstance: HINSTANCE;
    hIcon: HICON;
    hCursor: HCURSOR;
    hbrBackground: HBRUSH;
    lpszMenuName: LPCSTR;
    lpszClassName: LPCSTR;
}

@layout(platform)
struct POINT {
    x: int;
    y: int;
}

@layout(platform)
struct MSG {
    hwnd: HWND;
    message: uint;
    wParam: WPARAM;
    lParam: LPARAM;
    time: uint;
    pt: POINT;

    $: uint;
}

@extern("RegisterClassA")
def RegisterClassA(cls: *WNDCLASSA): ATOM;

@extern("UnregisterClassA")
def UnregisterClassA(lpClassName: str, hInstance: HINSTANCE): BOOL;

@extern("DefWindowProcA")
def DefWindowProcA(hWnd: HWND, uMsg: uint, wParam: WPARAM, lParam: LPARAM): LRESULT;

@extern("CreateWindowExA")
def CreateWindowExA(
    dwExStyle: uint,
    lpClassName: str,
    lpWindowName: str,
    dwStyle: uint,
    x: int, y: int,
    nWidth: int, nHeight: int,
    hWndParent: HWND,
    hMenu: HMENU,
    hInstance: HINSTANCE,
    lpParam: opaque
): HWND;

@extern("ShowWindow")
def ShowWindow(hWnd: HWND, nCmdShow: int): int;

@extern("UpdateWindow")
def UpdateWindow(hWnd: HWND): int;

@extern("DestroyWindow")
def DestroyWindow(hWnd: HWND): int;

@extern("PeekMessageA")
def PeekMessageA(
    lpMsg: *MSG,
    hWnd: HWND,
    wMsgFilterMin: uint,
    wMsgFilterMax: uint,
    wRemoveMsg: uint
): int;

@extern("GetMessageA")
def GetMessageA(
    lpMsg: *MSG,
    hWnd: HWND,
    wMsgFilterMin: uint,
    wMsgFilterMax: uint
): int;

@extern("TranslateMessage")
def TranslateMessage(lpMsg: *MSG): int;

@extern("DispatchMessageA")
def DispatchMessageA(lpMsg: *MSG): uint;

@extern("PostQuitMessage")
def PostQuitMessage(nExitCode: int): void;

const null = as<opaque>(0);

@entry(gui)
def main(hInstance: HINSTANCE, hPrevInstance: HINSTANCE, pCmdLine: *char, nCmdShow: int): int {
    const szAppName = "Hello";
    const szTitle = "Hello, World!";

    var wndclass: WNDCLASSA = .{
        style = (1 | 2),
        lpfnWndProc = windowCallback,
        cbClsExtra = 0,
        cbWndExtra = 0,
        hInstance = hInstance,
        hIcon = null,
        hCursor = null,
        hbrBackground = null,
        lpszClassName = szAppName,
        lpszMenuName = null
    };

    RegisterClassA(&wndclass);

    var hWnd = CreateWindowExA(
        0,
        szAppName,
        szTitle,
        0xcf0000,
        0, 0,
        640, 480,
        null,
        null,
        hInstance,
        null
    );

    ShowWindow(hWnd, nCmdShow);
    UpdateWindow(hWnd);

    var msg: MSG = noinit;
    while GetMessageA(&msg, null, 0, 0) != 0 {
        TranslateMessage(&msg);
        DispatchMessageA(&msg);
    }

    DestroyWindow(hWnd);
    UnregisterClassA(szAppName, hInstance);

    return msg.wParam;
}

def windowCallback(hWnd: HWND, uMsg: uint, wParam: WPARAM, lParam: LPARAM): LRESULT {
    if uMsg == 0x0002 { // WM_DESTROY
        PostQuitMessage(0);
        return 0;
    }

    return DefWindowProcA(hWnd, uMsg, wParam, lParam);
}
