project('cthulhu', 'c',
    license : 'LGPLv3',
    version : '0.2.7',
    meson_version : '>=1.3.0',
    default_options : [
        # gcc is picky about POSIX stdlib extensions
        'c_std=gnu11,c11',

        'cpp_std=c++20',

        # we use setjmp/longjmp for error handling
        # on msvc c++ exceptions do not work with setjmp/longjmp
        'cpp_eh=none',

        # we dont use rtti
        'cpp_rtti=false',

        'warning_level=3',
        'werror=true',

        # currently meson doesnt forward `declare_dependency:sources` between dependencies
        # which means we cant pass the ctu_xxx_api.h headers between dependencies
        # so we have to use static linking until thats fixed
        'default_library=static'
    ]
)

buildtype = get_option('buildtype')
is_debug = buildtype.contains('debug')
is_release = buildtype.contains('release')
cc = meson.get_compiler('c')
target = target_machine.system()

opt_assert = get_option('assert').disable_auto_if(is_release)

frontend_gui = get_option('frontend_gui')

# disable unit tests if we are a subproject or release build
# this is because they require some different compiler flags that slow down the compiler
# but only on gcc
unit_tests = get_option('unit_tests').disable_auto_if(is_release or meson.is_subproject())
driver_tests = get_option('driver_tests').disable_auto_if(meson.is_subproject())

opt_analyze = get_option('analyze')
opt_hardening = get_option('hardening')

warning_level = get_option('warning_level').to_int()

trace_memory = get_option('trace_memory').disable_auto_if(is_release or meson.is_subproject())

# these are tools for internal use, so disable them in release mode
tool_stacktrace = get_option('tool_stacktrace').disable_auto_if(is_release or meson.is_subproject())
tool_notify = get_option('tool_notify').disable_auto_if(is_release or meson.is_subproject())
tool_display = get_option('tool_display').disable_auto_if(is_release or meson.is_subproject())

# tools like this are user facing, so dont disable them in release mode
tool_diagnostic = get_option('tool_diagnostic').disable_auto_if(meson.is_subproject())
tool_cpreproc = get_option('tool_cpreproc').disable_auto_if(meson.is_subproject())

wants_cpp = frontend_gui.allowed() or tool_stacktrace.allowed()
needs_cpp = frontend_gui.enabled() or tool_stacktrace.enabled()

if wants_cpp
    has_cpp = add_languages('cpp', native : false, required : needs_cpp)

    frontend_gui = frontend_gui.require(has_cpp)
    tool_stacktrace = tool_stacktrace.require(has_cpp)
endif

frontend_gui = frontend_gui.require(target == 'windows')

if unit_tests.enabled() and is_release and cc.get_id() == 'gcc'
    warning('enabling unit tests in release mode with gcc will result in degraded performance')
endif

version = meson.project_version()
host = host_machine.system()
parts = version.split('.')

source_dir = meson.project_source_root() / 'src'
data_dir = meson.project_source_root() / 'data'

config_cdata = configuration_data({
    'CTU_MAJOR': parts[0].to_int(),
    'CTU_MINOR': parts[1].to_int(),
    'CTU_PATCH': parts[2].to_int()
})

config_cdata.set10('CTU_DEBUG', opt_assert.allowed())

# we use `__attribute__((const|pure))` heavily, but during unit tests we
# violate function preconditions to test assertions.
# msvc and clang dont mind, but gcc does. so we disable the attributes for gcc
config_cdata.set10('CTU_DISABLE_FN_PURITY', unit_tests.allowed() and cc.get_id() == 'gcc')

# gcc warns about __PRETY_FUNCTION__ not being ISO C when on warning level 3
# which is weird seeing as we specify gnu11, maybe its a gcc bug?
config_cdata.set10('CTU_HAS_PRETTY_FUNCTION', warning_level < 3)

config_cdata.set10('CTU_TRACE_MEMORY', trace_memory.allowed())

gmp = dependency('gmp', fallback : [ 'mini_gmp', 'gmp' ])

# args for all code
args = cc.get_supported_arguments(
    # we use flexible array members
    '/wd4200',

    # extraneous features that we dont use
    '-fstrict-aliasing',
    '-fno-threadsafe-statics',
    '-fno-keep-static-consts',
    '-fmerge-all-constants'
)

# args for handwritten code
user_args = cc.get_supported_arguments(
    # this isnt on by default in gcc
    '-Wnonnull'
)

# args for code generated by flex/bison
generated_args = cc.get_supported_arguments(
    '-Wno-unused-but-set-variable',
    '-Wno-unused-function',
    '-Wno-unused-parameter',

    # may need to update bison soon
    '-Wno-deprecated-non-prototype',

    # bison generates some unreachable code
    '/wd4702',

    # flex generates iffy code on mac
    '-Wno-sign-compare',

    # mingw needs this define for flex/bison to work
    '-D__USE_MINGW_ANSI_STDIO=1'
)

analyze_user_args = [
    # msvc analyze args
    '/analyze',
    '/analyze:max_paths 4096',

    # gcc analyze args
    '-fanalyzer',

    # gcc is a bit too aggressive with these
    '-Wno-analyzer-malloc-leak',

    # this one looks like a gcc bug
    '-Wno-analyzer-double-free'
]

if opt_analyze.enabled()
    user_args += cc.get_supported_arguments(analyze_user_args)
endif

harden_args = [
    # msvc hardening args
    '/GS', '/Gs', '/guard:cf',

    # gcc hardening args
    '-fstack-protector-all', '-fstack-clash-protection',
    '-fcf-protection=full',

    # other hardening args
    '-D_FORTIFY_SOURCE=2'
]

if opt_hardening.enabled()
    args += cc.get_supported_arguments(harden_args)
endif

add_project_arguments(args, language : [ 'c' ])

lexargs = []
parseargs = []

# setup required flex/bison args
if host == 'windows'
    lexargs += [ '--wincompat' ]
else
    parseargs += [ '-Wdeprecated' ]
endif

flex = find_program('flex', 'win_flex', version : '>=2.6')
bison = find_program('bison', 'win_bison', version : host == 'windows' ? '>=2.6' : '>=3.5')

lex = generator(flex,
    output : [ '@BASENAME@_flex.c', '@BASENAME@_flex.h' ],
    arguments : lexargs + [
        '--outfile=@OUTPUT0@',
        '--header-file=@OUTPUT1@',
        '@INPUT@'
    ]
)

parse = generator(bison,
    output : [ '@BASENAME@_bison.c', '@BASENAME@_bison.h' ],
    arguments : parseargs + [
        '-d', '@INPUT@', '-v',
        '--output=@OUTPUT0@',
        '--defines=@OUTPUT1@'
    ]
)

driver_summary = {}
langs = {}
interfaces = {}
plugins = {}

setargv = []
if cc.get_id() == 'msvc'
    setargv = [ 'setargv.obj' ]
endif

subdir('src')

if unit_tests.allowed()
    subdir('tests/unit')
endif

if driver_tests.allowed()
    subdir('tests/lang')
endif

doxygen = find_program('doxygen', required : get_option('doxygen'))

if doxygen.found()
    doxy_sources = [
        source_dir / 'interface',
        source_dir / 'common',
        source_dir / 'support',
        source_dir / 'cthulhu',
        source_dir / 'driver',

        data_dir / 'docs'
    ]

    stylesheets = [
        data_dir / 'doxygen-awesome.css',
        data_dir / 'doxygen-awesome-sidebar-only.css'
    ]

    doxy_includes = [
        source_dir / 'common',
        source_dir / 'cthulhu/events/include',
        source_dir / 'cthulhu/runtime/include',
        source_dir / 'cthulhu/ssa/include',
        source_dir / 'cthulhu/tree/include'
    ]

    doxy_cdata = configuration_data()
    doxy_cdata.merge_from(config_cdata)
    doxy_cdata.set('CTU_SOURCES', ' '.join(doxy_sources))
    doxy_cdata.set('ROOT_SOURCE_DIR', source_dir)
    doxy_cdata.set('INCLUDE_PATHS', ' '.join(doxy_includes))
    doxy_cdata.set('EXTRA_STYLESHEET', ' '.join(stylesheets))
    doxy_cdata.set('CLANG_DATABASE', meson.global_build_root() / 'compile_commands.json')

    doxy_cfg = configure_file(
        input : 'data/Doxyfile.in',
        output : 'Doxyfile',
        configuration : doxy_cdata
    )

    run_target('docs', command : [ doxygen, doxy_cfg ])
endif

summary = {
    'Build': {
        'Debug': is_debug,
        'Interfaces': ', '.join(get_option('interfaces')),
        'Languages': ', '.join(get_option('languages')),
        'Memory tracing': trace_memory.allowed(),
        'Analyze': opt_analyze.enabled(),
        'Hardening': opt_hardening.enabled(),
        'Assertions': opt_assert.allowed()
    },
    'Graphical Interface': {
        'Enabled': frontend_gui.allowed(),
        'Backend': 'd3d12+win32'
    },
    'Testing': {
        'Unit tests': unit_tests,
        'Driver tests': driver_tests
    },
    'Tools': {
        'Stacktrace': tool_stacktrace.allowed(),
        'Notify': tool_notify.allowed(),
        'Diagnostics': tool_diagnostic.allowed(),
        'Display': tool_display.allowed(),
        'C preprocessor': tool_cpreproc.allowed()
    },
    'Documentation': {
        'Doxygen': doxygen
    }
}

foreach lang_name, lang_config : langs
    if lang_config.has_key('fuzz_corpus')
        driver_summary += { lang_name + ' fuzz corpus': lang_config.get('fuzz_corpus') }
    endif
endforeach

summary += { 'Drivers': driver_summary }

foreach section_title, section : summary
    summary(section, bool_yn : true, section : section_title)
endforeach
